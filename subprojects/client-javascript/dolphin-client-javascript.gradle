// see also: https://github.com/eriwen/gradle-js-plugin
// Pull the plugin from Maven Central
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.8.0'
    }
}
// Invoke the plugin
apply plugin: 'js'

// Declare the js sources
javascript.source {
    dev {
        js {
            srcDir "js"
            include "**/*.js"
            exclude "**/*.min.js"
        }
    }
}

// does not work with Java 8 !
requireJs {
    source = javascript.source.dev.js.files
    dest = "${buildDir}/raw-opendolphin.js"
    requirejs.options = [
        name: "opendolphin",
        baseUrl: "js/dolphin",
        optimize: "none"            // comment this line for full ugly minification
    ]
} << {
    copy {
        from   "${buildDir}/raw-opendolphin.js"
        into   buildDir
        rename ".*", "opendolphin.js"
        filter { String line -> line.replaceAll(/\.{2}\/\.{2}\/js\/dolphin\//, '')}
    }
}

task updateGrailsJs << {
    ant.delete dir: '../../dolphin-grails/web-app/js/dolphin'
    copy {
        from 'js'
        into '../../dolphin-grails/web-app/js'
    }
    copy {
        from 'libs'
        into '../../dolphin-grails/web-app/libs'
    }
    copy {
        from "${buildDir}/opendolphin.js"
        into '../../dolphin-grails/web-app/dolphin'
    }
}

task jsZip (type: Zip) {
    archiveName    = 'opendolphin.js.zip'
    destinationDir = buildDir
    from '.', {
        include 'js/**'
        include 'libs/**'
    }
}

task install (dependsOn: [jsZip, requireJs, updateGrailsJs])